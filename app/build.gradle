apply plugin: "com.android.application"
apply plugin: "jp.leafytree.android-scala"

task wrapper(type: Wrapper) {
    gradleVersion = '5.4.1'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        applicationId 'com.btcontract.wallet'
        multiDexEnabled true
        targetSdkVersion 30
        minSdkVersion 21

        versionName '2.5.1'
        versionCode 98
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "SBW-${defaultConfig.versionName}.apk"
        }
    }

    packagingOptions {
        exclude 'META-INF/**'
    }
}

dependencies {
    // Android-specific
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.play:review:2.0.1'
    implementation 'com.cottacush:CurrencyEditText:0.0.10'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'com.guardanis:applock:3.0.2'

    implementation('com.journeyapps:zxing-android-embedded:4.2.0') {
        // We use zxing sources with fixed List.sort which is not in API21
        exclude group: 'com.google.zxing', module: 'core'
    }

    // Immortan
    implementation 'org.scala-lang:scala-library:2.11.12'
    implementation 'fr.acinq.secp256k1:secp256k1-kmp-jni-android:0.5.2'
    implementation 'org.scala-lang.modules:scala-parser-combinators_2.11:2.1.0'
    implementation 'com.softwaremill.quicklens:quicklens_2.11:1.7.5'
    implementation 'com.typesafe.akka:akka-actor_2.11:2.3.14'
    implementation 'org.scodec:scodec-core_2.11:1.11.4'
    implementation 'io.reactivex:rxscala_2.11:0.27.0'

    implementation 'org.json4s:json4s-native_2.11:3.6.7'
    implementation 'io.spray:spray-json_2.11:1.3.6'

    implementation 'org.bouncycastle:bcprov-jdk15to18:1.68'
    implementation 'com.google.guava:guava:29.0-android'
    implementation 'commons-codec:commons-codec:1.11'
    implementation 'io.netty:netty-all:4.1.42.Final'

    // Built-in Tor
    implementation 'com.squareup.okhttp3:okhttp:4.9.1'
    implementation 'info.guardianproject:tor-android:0.4.7.11'
    implementation 'info.guardianproject:jtorctl:0.4.5.7'
}
